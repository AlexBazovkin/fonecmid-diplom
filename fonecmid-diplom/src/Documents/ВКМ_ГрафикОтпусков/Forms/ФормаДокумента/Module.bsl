#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	МаксимальноеЧислоДнейОтпуска = Константы.ВКМ_МаксимальноеЧислоДнейОтпуска.Получить();
	ПроверитьДлительностьОтпусков();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников
&НаКлиенте
Процедура ОтпускаСотрудниковДатаНачалаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ОтпускаСотрудников.ТекущиеДанные;

	ДатыЗаполненыКорректно(ТекущиеДанные, "ДатаНачала");
	ПроверитьДлительностьОтпусков();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтпускаСотрудниковДатаОкончанияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ОтпускаСотрудников.ТекущиеДанные;
	
	ДатыЗаполненыКорректно(ТекущиеДанные, "ДатаОкончания");
	ПроверитьДлительностьОтпусков();
КонецПроцедуры



&НаКлиенте
Процедура ОтпускаСотрудниковСотрудникПриИзменении(Элемент)
	ПроверитьДлительностьОтпусков();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ЗаполнитьСотрудников(Команда)
	Если Объект.ОтпускаСотрудников.Количество() > 0 Тогда
		ЗаполнитьСотрудниковАсинх();
	Иначе
		ЗаполнитьСотрудниковНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АнализГрафика(Команда)
	
	Адрес = ПоместитьДанныеВоВременноеХранилище();
	
	Если Адрес <> Неопределено И ЗначениеЗаполнено(Адрес) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Адрес",Адрес);
		ОткрытьФорму("Документ.ВКМ_ГрафикОтпусков.Форма.АнализГрафика", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ДатыЗаполненыКорректно(ТекущиеДанные, ДатаНачалаОкончания)
	ДатыКорректны = Истина;
	НомерСтроки = Объект.ОтпускаСотрудников.Индекс(ТекущиеДанные) + 1;
	ПолеСообщения = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОтпускаСотрудников", НомерСтроки, ДатаНачалаОкончания);
	
	Если (ДатаНачалаОкончания = "ДатаНачала" И НачалоГода(ТекущиеДанные.ДатаНачала) <> НачалоГода(Объект.Год))
		Или (ДатаНачалаОкончания = "ДатаОкончания" И НачалоГода(ТекущиеДанные.ДатаОкончания) <> НачалоГода(Объект.Год)) Тогда
		ТекстСообщения = "Дата планирования отпуска находится за пределами планируемого года";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,ПолеСообщения ,"Объект" , );
		ДатыКорректны = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала) И ЗначениеЗаполнено(ТекущиеДанные.ДатаОкончания) Тогда
		Если ТекущиеДанные.ДатаНачала > ТекущиеДанные.ДатаОкончания Тогда
			ТекстСообщения = "Дата окончания отпуска предшествует дате начала";
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,ПолеСообщения ,"Объект" , );
			ДатыКорректны = Ложь;
		Иначе
			ТекущиеДанные.ДнейОтпуска = (НачалоДня(ТекущиеДанные.ДатаОкончания) - НачалоДня(ТекущиеДанные.ДатаНачала) + 86400) / 86400;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ДатыКорректны Тогда
		ТекущиеДанные.ДнейОтпуска = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьДлительностьОтпусков()
	
	Отказ = Ложь;
	Таблица = Объект.ОтпускаСотрудников.Выгрузить();
	Таблица.Свернуть("Сотрудник", "ДнейОтпуска");
	
	УсловноеОформление.Элементы.Очистить();
	
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();
		
		Если СтрокаТаблицы.ДнейОтпуска <=0 Тогда
			Отказ = Истина;
			Продолжить;
		ИначеЕсли СтрокаТаблицы.ДнейОтпуска < МаксимальноеЧислоДнейОтпуска Тогда  
			ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.ЦианСветлый);
			Отказ = Истина;
		ИначеЕсли СтрокаТаблицы.ДнейОтпуска > МаксимальноеЧислоДнейОтпуска Тогда
			ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.СветлоРозовый);
			Отказ = Истина;
		Иначе
			Продолжить
		КонецЕсли;
		
		ЭлементОформления.Использование = Истина;
		ЭлементУсловия = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементУсловия.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОтпускаСотрудников.Сотрудник");
		ЭлементУсловия.ПравоеЗначение = СтрокаТаблицы.Сотрудник;
		ЭлементУсловия.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементУсловия.Использование = Истина;

		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ОтпускаСотрудников");
		
	КонецЦикла;
	
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Использование = Истина;
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Красный);
	
	ЭлементУсловия = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементУсловия.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОтпускаСотрудников.ДнейОтпуска");
	ЭлементУсловия.ПравоеЗначение = 0;
	ЭлементУсловия.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
	ЭлементУсловия.Использование = Истина;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ОтпускаСотрудников");
	
	Возврат Отказ;

Конецфункции


&НаКлиенте
Асинх Процедура ЗаполнитьСотрудниковАсинх()
	ТекстВопроса = Стршаблон("Табличная часть документа будет очищена. Продолжить?");
	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Нет, "Внимание!");
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьСотрудниковНаСервере();
	ПроверитьДлительностьОтпусков();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСотрудниковНаСервере()
	
	УсловноеОформление.Элементы.Очистить();
	Объект.ОтпускаСотрудников.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ФизическиеЛица.Ссылка
		|ИЗ
		|	Справочник.ВКМ_ФизическиеЛица КАК ВКМ_ФизическиеЛица
		|ГДЕ
		|	ВКМ_ФизическиеЛица.Уволен = ЛОЖЬ";
	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Объект.ОтпускаСотрудников.Добавить().Сотрудник = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьДанныеВоВременноеХранилище()
	СтруктураДанных = Новый Структура;
	ДанныеТаблицы = Объект.ОтпускаСотрудников.Выгрузить();
	СтруктураДанных.Вставить("ДанныеТаблицы", ДанныеТаблицы);
	СтруктураДанных.Вставить("Год", Объект.Год);
	Адрес = ПоместитьВоВременноеХранилище(СтруктураДанных, УникальныйИдентификатор);
	Возврат Адрес
КонецФункции

#КонецОбласти