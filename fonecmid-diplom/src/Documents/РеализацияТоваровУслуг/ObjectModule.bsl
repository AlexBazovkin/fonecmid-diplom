
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОбработкаЗаказов.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	
	Движение = Движения.ОбработкаЗаказов.Добавить();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Договор = Договор;
	Движение.Заказ = Основание;
	Движение.СуммаОтгрузки = СуммаДокумента;

	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;

	Если ВКМ_ДовавитьДвиженияВРегистрВыставленныеЗаРаботыАкты() Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.Договор КАК Договор,
	               |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	               |	ЗаказПокупателя.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Товары,
	               |	ЗаказПокупателя.Услуги.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ТоварыОснования = Выборка.Товары.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), ТоварыОснования);
	КонецЦикла;
	
	УслугиОснования = Выборка.Услуги.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Услуги.Добавить(), УслугиОснования);
	КонецЦикла;
	
	Основание = ДанныеЗаполнения;
	
КонецПроцедуры



// Автозаполнение табличной части Услуги документа с договором типа абонентское обслуживание
// в соответствии с выполнеными за месяц работами
// 
// Параметры:
// РеквизитыДоговора - Структура - "ВидДоговора, ВКМ_СуммаАбонентскойПлаты" 
Процедура ВКМ_ВыполнитьАвтозаполнение(Договор) Экспорт
	
	ТекстСообщения = "";
	НоменклатураАбонентскаяПлата = Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить();
	Если Не ЗначениеЗаполнено(НоменклатураАбонентскаяПлата) Тогда
		ТекстСообщения = СтрШаблон("%1%2%3", ТекстСообщения, "Не задано значение константы ""ВКМ_НоменклатураАбонентскаяПлата""", Символы.ПС);
	КонецЕсли;
	
	НоменклатураРаботыСпециалиста = Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить();
	Если Не ЗначениеЗаполнено(НоменклатураРаботыСпециалиста) Тогда
		ТекстСообщения = СтрШаблон("%1%2%3", ТекстСообщения, "Не задано значение константы ""ВКМ_НоменклатураРаботыСпециалиста""", Символы.ПС);
	КонецЕсли;
	
	Если СтрДлина(ТекстСообщения) > 0 Тогда
		ТекстСообщения = СтрШаблон("%1%2", ТекстСообщения, "Автозаполнение невозможно");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Товары.Очистить();
	Услуги.Очистить(); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботыОстаткиИОбороты.КоличествоЧасовОборот, 0) + ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботыОстаткиИОбороты.КоличествоЧасовНачальныйОстаток, 0) КАК КоличествоЧасовОборот,
		|	ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботыОстаткиИОбороты.СуммаКОплатеОборот, 0) + ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботыОстаткиИОбороты.СуммаКОплатеНачальныйОстаток, 0) КАК СуммаКОплатеОборот,
		|	ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботыОстаткиИОбороты.Договор.ВКМ_СтоимостьЧасаРаботы, 0) КАК ДоговорВКМ_СтоимостьЧасаРаботы
		|ПОМЕСТИТЬ ВТ_Обороты
		|ИЗ
		|	РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы.ОстаткиИОбороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			,
		|			Договор = &Договор
		|				И Клиент = &Контрагент) КАК ВКМ_ВыполненныеКлиентуРаботыОстаткиИОбороты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботы.КоличествоЧасов, 0),
		|	ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботы.СуммаКОплате, 0),
		|	ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботы.Договор.ВКМ_СтоимостьЧасаРаботы, 0)
		|ИЗ
		|	РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы КАК ВКМ_ВыполненныеКлиентуРаботы
		|ГДЕ
		|	ВКМ_ВыполненныеКлиентуРаботы.Клиент = &Контрагент
		|	И ВКМ_ВыполненныеКлиентуРаботы.Договор = &Договор
		|	И ВКМ_ВыполненныеКлиентуРаботы.Регистратор = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВТ_Обороты.КоличествоЧасовОборот) КАК КоличествоЧасовОборот,
		|	СУММА(ВТ_Обороты.СуммаКОплатеОборот) КАК СуммаКОплатеОборот,
		|	ВТ_Обороты.ДоговорВКМ_СтоимостьЧасаРаботы КАК ДоговорВКМ_СтоимостьЧасаРаботы
		|ПОМЕСТИТЬ ВТ_Сгруппировано
		|ИЗ
		|	ВТ_Обороты КАК ВТ_Обороты
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Обороты.ДоговорВКМ_СтоимостьЧасаРаботы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_НоменклатураАбонентскаяПлата.Значение КАК Номенклатура,
		|	1 КАК Количество,
		|	ДоговорыКонтрагентов.ВКМ_СуммаАбонентскойПлаты КАК Цена,
		|	ДоговорыКонтрагентов.ВКМ_СуммаАбонентскойПлаты КАК Сумма
		|ИЗ
		|	Константа.ВКМ_НоменклатураАбонентскаяПлата КАК ВКМ_НоменклатураАбонентскаяПлата,
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Ссылка = &Договор
		|	И ДоговорыКонтрагентов.ВКМ_СуммаАбонентскойПлаты > 0
		|	И НЕ ДоговорыКонтрагентов.ПометкаУдаления
		|	И ДоговорыКонтрагентов.Организация = &Организация
		|	И ДоговорыКонтрагентов.Владелец = &Контрагент
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_НоменклатураРаботыСпециалиста.Значение,
		|	ЕСТЬNULL(ВТ_Сгруппировано.КоличествоЧасовОборот, 0),
		|	ЕСТЬNULL(ВТ_Сгруппировано.ДоговорВКМ_СтоимостьЧасаРаботы, 0),
		|	ЕСТЬNULL(ВТ_Сгруппировано.СуммаКОплатеОборот, 0)
		|ИЗ
		|	Константа.ВКМ_НоменклатураРаботыСпециалиста КАК ВКМ_НоменклатураРаботыСпециалиста,
		|	ВТ_Сгруппировано КАК ВТ_Сгруппировано";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Организация", Организация); 
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	                                                              
	РезультатЗапроса = Запрос.Выполнить();
	
	Если  РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли; 
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РезультатЗапроса.Выгрузить(), Услуги);
	СуммаДокумента = Услуги.Итог("Сумма");
	
КонецПроцедуры

Функция ВКМ_ДовавитьДвиженияВРегистрВыставленныеЗаРаботыАкты()
	Отказ = Ложь;
	ВидДоговора = Неопределено;
	АбонентскаяПлата = Неопределено;
	РаботыСпециалиста = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_НоменклатураАбонентскаяПлата.Значение КАК АбонентскаяПлата,
		|	ВКМ_НоменклатураРаботыСпециалиста.Значение КАК РаботыСпециалиста,
		|	РеализацияТоваровУслуг.Договор.ВидДоговора КАК ВидДоговора
		|ИЗ
		|	Константа.ВКМ_НоменклатураАбонентскаяПлата КАК ВКМ_НоменклатураАбонентскаяПлата,
		|	Константа.ВКМ_НоменклатураРаботыСпециалиста КАК ВКМ_НоменклатураРаботыСпециалиста,
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		АбонентскаяПлата = Выборка.АбонентскаяПлата;
		РаботыСпециалиста = Выборка.РаботыСпециалиста;
		ВидДоговора = Выборка.ВидДоговора;
	КонецЕсли;
	
	Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
		Движения.ВКМ_ВыставленныеЗаРаботыАкты.Записывать = Истина;

		СуммаРаботыСпециалиста = 0;
		СуммаАбонентскаяПлата = 0;
		
		Если Не ЗначениеЗаполнено(АбонентскаяПлата) Или Не ЗначениеЗаполнено(РаботыСпециалиста) Тогда
			ОбщегоНазначения.СообщитьПользователю("Проверьте заполнение костант ""ВКМ_НоменклатураАбонентскаяПлата"" и ""ВКМ_НоменклатураРаботыСпециалиста""");
			Отказ = Истина;
		КонецЕсли;
		
		Для Каждого ТекСтрокаУслуги Из Услуги Цикл
			Если ТекСтрокаУслуги.Номенклатура = РаботыСпециалиста Тогда
				СуммаРаботыСпециалиста = СуммаРаботыСпециалиста + ТекСтрокаУслуги.Сумма;
			ИначеЕсли ТекСтрокаУслуги.Номенклатура = АбонентскаяПлата Тогда
				 СуммаАбонентскаяПлата = СуммаАбонентскаяПлата + ТекСтрокаУслуги.Сумма;
			КонецЕсли;
		КонецЦикла;
		
		Движение = Движения.ВКМ_ВыставленныеЗаРаботыАкты.Добавить();
		Движение.Договор = Договор;
		Движение.Клиент = Контрагент;
		Движение.Период = Дата;
		Движение.СуммаАбонентскаяПлата = СуммаАбонентскаяПлата;
		Движение.СуммаРаботыСпециалиста = СуммаРаботыСпециалиста;
	КонецЕсли;
	Возврат Отказ;
КонецФункции

#КонецОбласти

#КонецЕсли
