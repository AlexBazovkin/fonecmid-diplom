
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции


// Определяем метод для указания настроек и сообщаем, что в документе есть обработчик 
// для добавления команд печати
// 
// Параметры:
//  НастройкиОбъекта - Структура - Настройки объекта
Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт
	// {{Еськин Д.Л. Доработка кода __ от 17.01.2025
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
	// }}Еськин Д.Л. Доработка кода __ от 17.01.2025
КонецПроцедуры

// Задаем идентификаторы печатных форм и представление
// 
// Параметры:
//  КомандыПечати - ТаблицаЗначений - Коллекция команд печати 
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	// {{Еськин Д.Л. Доработка кода __ от 17.01.2025
	// Акт об оказанных услугах
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОбОказанныхУслугах";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказанных услугах'");
	КомандаПечати.Порядок = 5;
	// }}Еськин Д.Л. Доработка кода __ от 17.01.2025
КонецПроцедуры		

 Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	// {{Еськин Д.Л. Доработка кода __ от 19.01.2025
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктОбОказанныхУслугах");
	Если ПечатнаяФорма <> Неопределено Тогда
		ПечатнаяФорма.ТабличныйДокумент = ВКМ_ПечатьАкта(МассивОбъектов, ОбъектыПечати);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказанных услугах'");
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ПФ_MXL_АктОбОказанныхУслугах";
	КонецЕсли;	
	// }}Еськин Д.Л. Доработка кода __ от 19.01.2025
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВКМ_ПечатьАкта(МассивОбъектов, ОбъектыПечати)
	
	// {{Еськин Д.Л. Доработка кода __ от 19.01.2025
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "АктОбОказанныхУслугах";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_АктОбОказанныхУслугах");
	
	ДанныеДокументов = ВКМ_ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВКМ_ВывестиЗаголовокАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВКМ_ВывестиРеквизитыСторон(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВКМ_ВывестиТоварыУслуги(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВКМ_ВывестиПодвал(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;	
	// }}Еськин Д.Л. Доработка кода __ от 19.01.2025
	
КонецФункции

Функция ВКМ_ПолучитьДанныеДокументов(МассивОбъектов)
	
	// {{Еськин Д.Л. Доработка кода __ от 19.01.2025
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Номер,
	|	РеализацияТоваровУслуг.Дата,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.Организация) КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.Контрагент) КАК Контрагент,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.Договор) КАК Договор,
	|	РеализацияТоваровУслуг.СуммаДокумента,
	|	РеализацияТоваровУслуг.СуммаДокумента,
	|	РеализацияТоваровУслуг.Товары.(
	|		Ссылка,
	|		НомерСтроки,
	|		Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма),
	|	РеализацияТоваровУслуг.Услуги.(
	|		Ссылка,
	|		НомерСтроки,
	|		Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма)
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В (&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	// }}Еськин Д.Л. Доработка кода __ от 19.01.2025
	
КонецФункции

Процедура ВКМ_ВывестиЗаголовокАкта(ДанныеДокументов, ТабличныйДокумент, Макет)
	// {{Еськин Д.Л. Доработка кода __ от 19.01.2025
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	
	ДанныеПечати = Новый Структура;
	
	НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер);
	ДанныеПечати.Вставить("НомерДокумента", НомерДокумента);	
	ДатаДокумента = Формат(ДанныеДокументов.Дата, "ДЛФ=DD");
	ДанныеПечати.Вставить("ДатаДокумента", ДатаДокумента);
	
	ОбластьЗаголовок.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);	
	// }}Еськин Д.Л. Доработка кода __ от 19.01.2025
	
КонецПроцедуры


Процедура ВКМ_ВывестиРеквизитыСторон(ДанныеДокументов, ТабличныйДокумент, Макет)
	// {{Еськин Д.Л. Доработка кода __ от 19.01.2025
	ОбластьРеквизитыШапки = Макет.ПолучитьОбласть("РеквизитыШапки");
	
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("Организация", ДанныеДокументов.Организация);
	ДанныеПечати.Вставить("Контрагент", ДанныеДокументов.Контрагент);
	ДанныеПечати.Вставить("Договор", ДанныеДокументов.Договор);
	
	ДанныеQRКОда = ГенерацияШтрихкода.ДанныеQRКода(ДанныеДокументов.Ссылка, 1, 120);
	КартинкаQRКода = Новый Картинка(ДанныеQRКОда);
	ОбластьРеквизитыШапки.Рисунки.КартинкаQR.Картинка = КартинкаQRКода;
	
	ОбластьРеквизитыШапки.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьРеквизитыШапки);	
	// }}Еськин Д.Л. Доработка кода __ от 19.01.2025
	
КонецПроцедуры

Процедура ВКМ_ВывестиТоварыУслуги(ДанныеДокументов, ТабличныйДокумент, Макет)
	// {{Еськин Д.Л. Доработка кода __ от 19.01.2025
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	ЕдиницНоменклатуры = 0;
	ВыборкаТовары = ДанныеДокументов.Товары.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		ЕдиницНоменклатуры = ЕдиницНоменклатуры + 1;
		ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	ВыборкаУслуги = ДанныеДокументов.Услуги.Выбрать();
	Пока ВыборкаУслуги.Следующий() Цикл
		ЕдиницНоменклатуры = ЕдиницНоменклатуры + 1;
		ОбластьСтрока.Параметры.Заполнить(ВыборкаУслуги);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;	

	Итого = ДанныеДокументов.СуммаДокумента;
		
	ОбластьПодвалТаблицы = Макет.ПолучитьОбласть("ПодвалТаблицы");
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("Итого", Итого);	
	ОбластьПодвалТаблицы.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьПодвалТаблицы);	

	СуммаПрописью = ЧислоПрописью(Итого, "Л=ru_RU;ДП=Истина", "рубль,рубля,рублей,м,копейка,копейки,копеек,ж,2");
			
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("КоличествоСтрок", ЕдиницНоменклатуры);		
	ДанныеПечати.Вставить("Итого", Итого);
	ДанныеПечати.Вставить("СуммаПрописью", СуммаПрописью);	
	ОбластьИтого.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьИтого);	
	// }}Еськин Д.Л. Доработка кода __ от 19.01.2025
	
КонецПроцедуры

Процедура ВКМ_ВывестиПодвал(ДанныеДокументов, ТабличныйДокумент, Макет)
	// {{Еськин Д.Л. Доработка кода __ от 19.01.2025
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ТабличныйДокумент.Вывести(ОбластьПодвал);	
	// }}Еськин Д.Л. Доработка кода __ от 19.01.2025
	
КонецПроцедуры


#КонецОбласти


#КонецЕсли