#Область ПрограммныйИнтерфейс

// Отправить сообщение из справочника ВКМ_УведомленияТелеграмБоту в 
// Телеграмм с последующим удалением отправленных элементов.
Процедура ВКМ_ОтправитьСообщенияВТелеграмм() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_УведомленияТелеграмБоту.Ссылка,
		|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения
		|ИЗ
		|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту";
	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		РезультатОтправки = ОтправитьСообщение(ВыборкаДетальныеЗаписи.ТекстСообщения);		
		Если РезультатОтправки<> Неопределено И РезультатОтправки.КодСостояния = 200 Тогда
			СправочникОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			СправочникОбъект.Удалить();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


// Отправить сообщение ТекстСообщения в Телеграмм.
// 
// Параметры:
//  ТекстСообщения - Строка - Текст сообщения
// 
// Возвращаемое значение:
//  HTTPОтвет - HTTPОтвет сервера
Функция ОтправитьСообщение(ТекстСообщения) Экспорт
	
	ПараметрыТГ = ПрочитатьПараметрыСоединения();
	
	Если Не ПараметрыТГ.ПараметрыЗаполнены Тогда
		ЗаписьЖурналаРегистрации("Ошибка отправки сообщения в Telegram", УровеньЖурналаРегистрации.Ошибка, , , "Не заполнены параметры интеграции с Telegram.", );		
		Возврат Неопределено;
	КонецЕсли;
	
	Соединение = Новый HTTPСоединение("api.telegram.org", 443, , , , ПараметрыТГ.Таймаут, Новый ЗащищенноеСоединениеOpenSSL());	
	АдресРесурса = СтрШаблон("/bot%1/sendMessage", ПараметрыТГ.Токен);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	ЗапросКСерверу = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	СтруктураССообщением = Новый Структура;
	СтруктураССообщением.Вставить("chat_id", ПараметрыТГ.IDГруппы);
	СтруктураССообщением.Вставить("text", ТекстСообщения);
	
	JSONСтрока = ЗаписатьЗначениеJSON(СтруктураССообщением);
	ЗапросКСерверу.УстановитьТелоИзСтроки(JSONСтрока);
	
	Ответ = Соединение.ВызватьHTTPМетод("POST", ЗапросКСерверу);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ЗаписьЖурналаРегистрации("Ошибка отправки сообщения в Telegram", УровеньЖурналаРегистрации.Ошибка, , , Ответ.ПолучитьТелоКакСтроку(), );
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция ПрочитатьПараметрыСоединения()
	
	ПараметрыЗаполнены = Истина;
		
	Таймаут = Константы.ВКМ_ТаймаутСоединения.Получить();
	Если Не ЗначениеЗаполнено(Таймаут) Тогда
		Таймаут = 0;
	КонецЕсли;
	
	Токен = Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();
	Если Не ЗначениеЗаполнено(Токен) Тогда
		ПараметрыЗаполнены = Ложь;	
	КонецЕсли;
		
	
	IDГруппы = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	Если Не ЗначениеЗаполнено(IDГруппы) Тогда
		ПараметрыЗаполнены = Ложь;
	КонецЕсли;
		
	Параметры = Новый Структура;
	Параметры.Вставить("Таймаут", Таймаут);
	Параметры.Вставить("Токен", Токен);
	Параметры.Вставить("IDГруппы", IDГруппы);
	Параметры.Вставить("ПараметрыЗаполнены", ПараметрыЗаполнены);	
		
	Возврат Параметры;
	
КонецФункции

#КонецОбласти	