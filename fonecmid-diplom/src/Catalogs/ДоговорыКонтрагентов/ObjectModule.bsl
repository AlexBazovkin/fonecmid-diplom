#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий
Процедура ПередЗаписью(Отказ)
	
	// {{Еськин Д.Л. Удаление остаточных реквизитов абонентского договора 
	Если ОбменДанными.Загрузка Тогда
    	Возврат;
	КонецЕсли;

	Если ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание") Тогда
		ВКМ_ДатаНачалаДействияДоговора = Неопределено;
		ВКМ_ДатаОкончанияДействияДоговора = Неопределено;
		ВКМ_СтоимостьЧасаРаботы = Неопределено;
		ВКМ_СуммаАбонентскойПлаты = Неопределено;
	КонецЕсли;
	// }}Еськин Д.Л. Удаление остаточных реквизитов абонентского договора 

КонецПроцедуры


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// {{Еськин Д.Л. Проверка заполнения реквизитов договора абонентского обслуживания	
	РеквизитыАбонентскогоОбслуживания = Новый Массив;
	РеквизитыАбонентскогоОбслуживания.Добавить("ВКМ_ДатаНачалаДействияДоговора");
	РеквизитыАбонентскогоОбслуживания.Добавить("ВКМ_ДатаОкончанияДействияДоговора");
	РеквизитыАбонентскогоОбслуживания.Добавить("ВКМ_СуммаАбонентскойПлаты");
	РеквизитыАбонентскогоОбслуживания.Добавить("ВКМ_СтоимостьЧасаРаботы");
		
	Если ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание") Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПроверяемыеРеквизиты, РеквизитыАбонентскогоОбслуживания, Истина);
		Если ВКМ_ДатаОкончанияДействияДоговора < ВКМ_ДатаНачалаДействияДоговора Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Дата начала действия договора меньше даты окончания!'"));
			Отказ = Истина;
		КонецЕсли;
	Иначе
		ПроверяемыеРеквизиты = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ПроверяемыеРеквизиты, РеквизитыАбонентскогоОбслуживания);
	КонецЕсли;
	// }}Еськин Д.Л. Проверка заполнения реквизитов договора абонентского обслуживания
		
КонецПроцедуры
#КонецОбласти
	
#КонецЕсли